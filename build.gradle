// ========== GRADLE PLUGINS / REPOSITORIES SETUP
buildscript {
    ext.kotlinVersion = '1.2.21'

    repositories {
        jcenter()
    }
    dependencies {
        // STUFF FOR KOTLIN
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"

        // STUFF FOR SHADOW JAR - this builds the fat jar that contains the final application for deployment
        classpath 'com.github.jengelman.gradle.plugins:shadow:5.1.0'
    }
}

apply plugin: 'application'

// Define args for the application plugin so that `./gradlew run` launches the app with the correct system properties.
//      NOTE: We have to define these here, between the application and shadow plugins, due to: https://github.com/johnrengelman/shadow/issues/336
mainClassName = "com.myorg.MainKt"
applicationDefaultJvmArgs = [ "-D@appId=" + project.name , "-D@environment=local" ]

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'kotlin'

// All modules should apply the following bits
allprojects {
    apply plugin: 'java'
    apply plugin: 'kotlin'
    apply plugin: 'maven'
    apply plugin: 'project-report'
    apply plugin: 'idea'

    // Include source jar
    task sourcesJar(type: Jar, dependsOn: classes) {
        archiveClassifier.set('sources')
        from sourceSets.main.allSource
    }

    artifacts {
        archives sourcesJar
    }

    // Tell IntelliJ to download sources
    idea {
        module {
            downloadSources = true
        }
    }

    // Define the repos used for finding dependencies.
    repositories {
        jcenter()
    }

    test {
        // Minimize console spam while running tests without swallowing critical debugging info.
        testLogging {
            exceptionFormat "FULL"
            events "passed", "skipped", "failed"
            displayGranularity = 0
            showExceptions true
            showCauses true
            showStackTraces true
        }

        ignoreFailures = false
    }

    //http://www.gradle.org/docs/current/userguide/java_plugin.html
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    compileKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }
    compileTestKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }
}

run {
    // Make sure system properties included in the gradle task are piped through to the application.
    systemProperties.putAll(System.getProperties())
}

artifacts {
    archives shadowJar
}

// ========== PROPERTIES AND DEPENDENCIES
ext {
    // DEPENDENCY VERSIONS
    riposteVersion = '0.18.0'
    backstopperVersion = '0.12.0'
    slf4jVersion = '1.7.25'
    logbackVersion = '1.2.3'
    jacksonVersion = '2.9.4'
    kotlinGuiceVersion = '1.0.0'
    googleFindbugsJsr305Version = '3.0.2'
    jetbrainsAnnotationsVersion = '17.0.0'
    groovyVersion = '2.4.7'

    validationApiVersion = '2.0.1.Final'
    hibernateValidatorVersion = '6.0.17.Final'
    elApiVersion = '3.0.1-b06'
    elImplVersion = '3.0.1-b11'
    jaxbApiVersion = '2.4.0-b180830.0359'

    junitVersion = '4.12'
    mockitoVersion = '1.10.19'
    mockitoKotlinVersion = '1.5.0'
    assertJVersion = '3.9.0'
    assertJKotlinBridgeVersion = '0.2.1'
    junitDataproviderVersion = '1.13.1'
    restAssuredVersion = '3.0.7'

    // TODO: EXAMPLE CLEANUP - Remove the future-converter-java8-guava dependency version below unless you need it in your app
    futureConverterJava8GuavaVersion = '1.0.0'
    // TODO: EXAMPLE CLEANUP - Remove the cassandra related dependency versions below unless you need them in your app
    apacheCassandraVersion = '3.11.4'
    datastaxCassandraDriverVersion = '3.7.1'
    // TODO: EXAMPLE CLEANUP - Remove the embedded cassandra dependency below
    cassandraUnitVersion = '3.11.2.0'

    // JACOCO PROPERTIES
    jacocoToolVersion = '0.8.4'
    // Anything in this jacocoExclusions list will be excluded from coverage reports. The format is paths to class
    //      files, with wildcards allowed. e.g.: jacocoExclusions = [ "com/nike/Foo.class", "**/Bar*.*" ]
    jacocoExclusions = ['setup*.*', '**/Example*Endpoint*.*']
    jacocoCoverageThresholdSetup = {
        // You can filter to only specific submodules by adjusting the code in the findAll { [condition] } code block.
        //      (The logic is farmed out to the isSubprojectIncludedInJacocoReports(Project) function, since it is
        //      reused in multiple places).
        // You can also run the coverage checks on the combo report rather than submodule reports by removing the
        //      configure(...) block entirely, thus putting the jacocoCoverage task at the root level.
        configure(subprojects.findAll { isSubprojectIncludedInJacocoReports(it) }) {
            // Configure the minimum code coverage rules.
            jacocoTestCoverageVerification { JacocoCoverageVerification v ->
                violationRules {
                    rule { JacocoViolationRule r ->
                        enabled = true
                        limit {
                            minimum = 0.8
                            counter = "INSTRUCTION"
                        }

                    }

                    rule { JacocoViolationRule r ->
                        enabled = true
                        limit {
                            minimum = 0.8
                            counter = "BRANCH"
                        }
                    }
                }
            }
        }
    }
    // Configure which subprojects we're doing jacoco for.
    isSubprojectIncludedInJacocoReports = { Project subProj ->
        // For this repo we'll include everything that's not the remote-tests submodule.
        return !subProj.getName().contains("remote-tests")
    }
}

allprojects {
    configurations.all {
        // Other libraries like to include random versions of junit and mockito. Make sure we force the versions we want.
        resolutionStrategy.force "junit:junit:$junitVersion"
        resolutionStrategy.force "org.mockito:mockito-core:$mockitoVersion"
        // Ditto on groovy
        resolutionStrategy.force "org.codehaus.groovy:groovy-all:$groovyVersion"

        // Enforce the version of Kotlin we want
        resolutionStrategy.force "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
        resolutionStrategy.force "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
        resolutionStrategy.force "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"

        // Exclude the kotlin-stdlib-jre* dependencies - we are pulling in the recommended kotlin-stdlib-jdk8 ourselves,
        //      and the jre* libs are deprecated (the jdk* libs are the replacements).
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jre8'
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jre7'

        // Don't accidentally transitively pull in SLF4J implementations we don't want to use from other libraries.
        //      NOTE: If you'd prefer to use one of these rather than logback then you'll need to remove the exclusion
        //      for the lib you want and exclude logback instead.
        exclude group: 'org.slf4j', module: 'slf4j-simple'
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
	    exclude group: 'log4j'
        exclude group: 'commons-logging', module: 'commons-logging'

        // com.codahale.metrics artifacts conflict with the io.dropwizard metrics stuff Riposte uses.
        exclude group: 'com.codahale.metrics'
    }
}

dependencies {
    compile(
        project(':' + rootProject.name + '-core-code')
    )
}

// ========== COMBO TEST REPORT - View the combined/merged report at: [project_root]/build/reports/tests/index.html
apply from: file(rootProject.projectDir.getAbsolutePath() + '/gradle/junitComboTestReport.gradle')

// ========== JACOCO SETUP - View the combined/merged report at: [project_root]/build/reports/jacoco/jacocoRootReport/html/index.html.
//                           Individual reports for each submodule can be found at: [project_root]/[submodule]/build/reports/jacoco/test/html/index.html
apply from: file(rootProject.projectDir.getAbsolutePath() + '/gradle/jacoco.gradle')

allprojects {
    group = groupId // Necessary for the maven install task to function correctly

    task versionInfo {
        doLast {
            def versionfile = new File("$buildDir/version.properties")
            versionfile << "release.version=$artifactId-$version"
        }
    }

    build.dependsOn versionInfo
}

shadowJar {
    doFirst {
        println("The Executable jar should be created at build/libs/${rootProject.name}-${archiveVersion.get()}.jar")
    }

    // Disable the "-all" portion of the default shadow jar name.
    //      We want the output to be "${rootProject.name}-${version}.jar".
    //      If you'd prefer the default shadow jar name then you can remove this line.
    archiveClassifier.set('')
}

// The root level shouldn't generate the normal jar and source jars, just the shadow jar. Disable the jar and source jar
//      tasks for the root level project (this won't affect submodules, where we *do* want jars created)
jar.enabled = false
sourcesJar.enabled = false


// TODO: EXAMPLE CLEANUP - This replaceTemplate task can be removed.
task replaceTemplate {
    description = 'Run Replace Template Groovy script'

    doFirst {
        System.setProperty("rootProjectLocation", rootProject.projectDir.getAbsolutePath())

        evaluate(new File(rootProject.projectDir.getAbsolutePath() + "/riposte-microservice-template-core-code/src/main/groovy/setup.groovy"))
    }
}

