evaluationDependsOn(':')

dependencies {

    testImplementation (
        project(":riposte-microservice-template-core-code"),
        project(":riposte-microservice-template-core-code").sourceSets.test.output,
        "org.slf4j:slf4j-api:$slf4jVersion",
        "org.slf4j:jcl-over-slf4j:$slf4jVersion",
        "org.slf4j:log4j-over-slf4j:$slf4jVersion",
        "ch.qos.logback:logback-classic:$logbackVersion",
        "ch.qos.logback:logback-core:$logbackVersion",
        "org.assertj:assertj-core:$assertJVersion",
        "org.junit.jupiter:junit-jupiter-api:$junit5Version",
        "org.junit.jupiter:junit-jupiter-engine:$junit5Version",
        "org.junit.jupiter:junit-jupiter-params:$junit5Version",
        "org.mockito:mockito-core:$mockitoVersion",
        "io.rest-assured:rest-assured:$restAssuredVersion",

        "com.nike.riposte:riposte-typesafe-config:$riposteVersion",
        "com.nike.riposte:riposte-guice-typesafe-config:$riposteVersion",

        // Kotlin stuff
        "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion",
        "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion",
        "net.wuerl.kotlin:assertj-core-kotlin:$assertJKotlinBridgeVersion",
        "com.nhaarman.mockitokotlin2:mockito-kotlin:$mockitoKotlinVersion",
    )
}

test {
    // Exclude functional tests by default - force functional tests to run only via the functionalTest task
    exclude '**/functionaltest/**'
}

task functionalTest(type: Test, dependsOn: "testClasses") {
    description = "This is a task to run Functional Tests"
    include '**/functionaltest/**'
    useJUnitPlatform()

    testLogging {
        exceptionFormat "FULL"
        events "started", "passed", "skipped", "failed", "standardOut", "standardError"
        displayGranularity = 0
    }

    testLogging {
        exceptionFormat "FULL"
        events "started", "passed", "skipped", "failed", "standardOut", "standardError"
        displayGranularity = 0
    }

    doFirst {
        String remoteTestEnv = System.getProperty('remoteTestEnv')
        if (remoteTestEnv == null) {
            throw new GradleException('ERROR: You must specify the remoteTestEnv System property when running ' +
                                      'functional tests. Valid options are: local, test, or prod. e.g. -DremoteTestEnv=test')
        }

        systemProperties.putAll(System.getProperties())
        // Hack to fix jacoco blowing up our tests
        // See https://discuss.gradle.org/t/getting-classnotfoundexception-org-jacoco-agent-rt-internal-9dd1198-premain-with-1-7s-jacoco-plugin/1444
        // See also https://issues.gradle.org/browse/GRADLE-2859
        systemProperties['user.dir'] = workingDir
    }

    // Force this task to always run when specified (i.e. prevent gradle from saying "this task is up-to-date so I don't need to run it")
    outputs.upToDateWhen { false }
}